plugins {
    id 'com.flipkart.java.code.standard' version '0.1.2-SNAPSHOT'
}

ext {
    clojarsusername = project.properties['clojarsusername'] ?: ""
    clojarspassword = project.properties['clojarspassword'] ?: ""
    krystal_version = '8.0.0'
}

checkerFramework {
    extraJavacArgs = [
            "-Astubs=${rootDir}/config/checker/stubs",
    ]
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'idea'

    group 'com.flipkart.krystal'
    version project.krystal_version

    publishing {
        repositories {
            maven {
                url "https://clojars.org/repo"
                credentials {
                    username = rootProject.ext.clojarsusername
                    password = rootProject.ext.clojarspassword
                }
            }
        }
        publications.withType(MavenPublication).configureEach {
            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
}

configure(subprojects.findAll { it.name != 'krystal-bom' }) {
    apply plugin: 'java-library'
    apply plugin: 'com.flipkart.java.code.standard'
    apply plugin: 'jacoco'

    dependencies {
        implementation 'com.google.guava:guava'
        implementation 'org.checkerframework:checker-qual'
        implementation 'org.slf4j:slf4j-api'
        implementation 'jakarta.inject:jakarta.inject-api'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testImplementation 'org.assertj:assertj-core'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'

        api platform('org.junit:junit-bom:5.10.1')
        api platform('org.assertj:assertj-bom:3.25.2')
        api platform('io.github.resilience4j:resilience4j-bom:2.2.0')
        api platform('com.google.inject:guice-bom:7.0.0')
        api platform('org.slf4j:slf4j-bom:2.0.13')
        constraints {
            def jackson_version = '2.14.3'
            implementation 'com.fasterxml.jackson.core:jackson-annotations:' + jackson_version
            implementation 'com.fasterxml.jackson.core:jackson-databind:' + jackson_version
            implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:' + jackson_version
            implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:' + jackson_version
            implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:' + jackson_version

            implementation 'org.yaml:snakeyaml:2.2'

            implementation 'com.google.guava:guava:33.0.0-jre'
            implementation 'org.checkerframework:checker-qual:3.42.0'
            implementation 'org.slf4j:slf4j-api:2.0.11'
            implementation 'jakarta.inject:jakarta.inject-api:2.0.1'
            implementation 'org.reflections:reflections:0.10.2'
            implementation 'com.squareup:javapoet:1.13.0'
            implementation 'commons-cli:commons-cli:1.6.0'

            implementation 'com.google.auto.service:auto-service:1.1.1'
            annotationProcessor 'com.google.auto.service:auto-service:1.1.1'

            annotationProcessor 'com.flipkart.krystal:vajram-codegen:' + krystal_version
            testAnnotationProcessor 'com.flipkart.krystal:vajram-codegen:' + krystal_version

            String lombok_version = '1.18.30'
            implementation 'org.projectlombok:lombok:' + lombok_version
            annotationProcessor 'org.projectlombok:lombok:' + lombok_version
            testAnnotationProcessor 'org.projectlombok:lombok:' + lombok_version
        }
    }

    checkerFramework {
        extraJavacArgs = ["-Astubs=${rootDir}/config/checker/stubs",]
        excludeTests = true
    }

    jacoco {
        toolVersion = "0.8.11"
    }

    java {
        withSourcesJar()
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += '-Xlint:unchecked'
        options.deprecation = true
    }
    tasks.named('sourcesJar').configure { mustRunAfter('compileJava') }

}