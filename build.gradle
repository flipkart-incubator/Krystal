plugins {
    id 'com.diffplug.spotless' version '6.16.0'
    id 'org.checkerframework' version '0.6.30'
}

ext {
    clojarsusername = project.properties['clojarsusername'] ?: ""
    clojarspassword = project.properties['clojarspassword'] ?: ""
    krystal_version = '3.15-SNAPSHOT'
}

checkerFramework {
    checkers = [
            'org.checkerframework.checker.nullness.NullnessChecker',
    ]
    extraJavacArgs = [
            '-AstubDebug',
            "-Astubs=${rootDir}/config/checker/stubs",
    ]
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'com.diffplug.spotless'
    apply plugin: 'org.checkerframework'

    spotless {
        java {
            target 'src/*/java/**/*.java', 'build/generated/sources/vajrams/**/*.java'
            googleJavaFormat()
        }
    }

    afterEvaluate {
        tasks.named('spotlessCheck') {
            dependsOn('spotlessApply')
        }
    }

    checkerFramework {
        checkers = [
                'org.checkerframework.checker.nullness.NullnessChecker',
        ]
        extraJavacArgs = [
                "-Astubs=${rootDir}/config/checker/stubs",
        ]
        excludeTests = true
    }

    dependencies {
        implementation 'com.google.guava:guava:31.1-jre'
        implementation 'org.checkerframework:checker-qual:3.37.0'
        implementation 'org.slf4j:slf4j-api:2.0.3'

        compileOnly 'org.projectlombok:lombok:1.18.24'
        annotationProcessor 'org.projectlombok:lombok:1.18.24'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
        testImplementation 'org.assertj:assertj-core:3.23.1'

        testCompileOnly 'org.projectlombok:lombok:1.18.24'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
    }

    java {
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                url "https://clojars.org/repo"
                credentials {
                    username = rootProject.ext.clojarsusername
                    password = rootProject.ext.clojarspassword
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }
}