plugins {
    id 'java'
    id 'application'
    id 'com.flipkart.krystal' version "${krystal_version}"
    id 'com.flipkart.java.code.standard' version "${java_code_std_version}"
    id 'io.quarkus' version "${quarkus_version}"
}

dependencies {
    implementation platform("com.flipkart.krystal:lattice-bom:${lattice_version}")
    implementation platform("io.quarkus.platform:quarkus-bom:${quarkus_version}")
    krystalModelsGenProcessor platform("com.flipkart.krystal:lattice-bom:${lattice_version}")

    implementation 'com.flipkart.krystal:lattice-core'
    implementation 'com.flipkart.krystal:lattice-cdi'
    implementation 'com.flipkart.krystal:lattice-rest-quarkus'
    implementation 'com.flipkart.krystal:lattice-graphql-rest'
    implementation 'com.flipkart.krystal:vajram-json'
    implementation 'com.flipkart.krystal:vajram-java-sdk'
    implementation 'com.flipkart.krystal:vajram-graphql'
    implementation 'com.graphql-java:graphql-java:24.3'
    implementation 'com.graphql-java:graphql-java-extended-scalars:24.0'
    implementation 'io.quarkus:quarkus-smallrye-context-propagation'

    krystalModelsGenProcessor 'com.flipkart.krystal:vajram-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:lattice-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:lattice-cdi-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:vajram-json-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:lattice-json-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:lattice-rest-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:lattice-rest-quarkus-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:lattice-quarkus-arc-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:vajram-graphql-codegen'
    krystalModelsGenProcessor 'com.flipkart.krystal:lattice-graphql-codegen'


    runtimeOnly 'jakarta.servlet:jakarta.servlet-api'
    implementation 'jakarta.inject:jakarta.inject-api'

    compileOnly "org.projectlombok:lombok:$lombok_version"
    annotationProcessor "org.projectlombok:lombok:$lombok_version"

}

tasks.withType(JavaCompile).configureEach {
    source(files('src/main/graphqls'))
}

sourceSets {
    main {
        resources {
            // Add the custom directory to the resource source path
            srcDirs 'src/main/graphqls'
        }
    }
}

// Define the Custom Copy Task
tasks.register('extractGraphqls', Copy) {
    def graphqlsDependency = configurations.compileClasspath.find {
        it.name.contains('vajram-graphql')
    }
    if (graphqlsDependency == null) {
        throw new GradleException("Could not find vajram-graphql JAR in compileClasspath")
    }
    // Use zipTree to treat the JAR file as a directory tree
    from zipTree(graphqlsDependency)

    // Filter to only copy the required files (e.g., all .graphqls files)
    include '**/*.graphqls'

    // Destination: Copy them into your main resources output folder.
    // This makes them available on the classpath.
    into sourceSets.main.output.resourcesDir

    // Configure task inputs/outputs for up-to-date checking
    inputs.files(graphqlsDependency)
    outputs.dir(sourceSets.main.output.resourcesDir)
}

// Ensure the copy task runs before resource processing
processResources.configure {
    dependsOn('extractGraphqls')
}

tasks.named('compileQuarkusTestGeneratedSourcesJava') {
    enabled = false
}

configurations {
    krystalModelsGenProcessorPath {
        attributes {
            attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category.class, Category.LIBRARY));
            attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage.class, Usage.JAVA_RUNTIME));
            attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements.class, LibraryElements.JAR));
            attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling.class, Bundling.EXTERNAL));
            attribute(TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE, objects.named(TargetJvmEnvironment.class, TargetJvmEnvironment.STANDARD_JVM));
        }
    }
}