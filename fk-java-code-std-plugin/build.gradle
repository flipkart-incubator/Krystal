buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
}

ext {
    clojarsusername = project.properties['clojarsusername'] ?: ""
    clojarspassword = project.properties['clojarspassword'] ?: ""
}

group 'com.flipkart.java.code.standard'
version '5.2'

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'com.flipkart.java.code.standard'
            implementationClass = 'com.flipkart.java.code.FkJavaCodeStandardPlugin'
        }
    }
}

dependencies {
    implementation 'org.checkerframework:checkerframework-gradle-plugin:0.6.61'
    implementation 'com.diffplug.spotless:spotless-plugin-gradle:8.0.0'
    implementation 'net.ltgt.gradle:gradle-errorprone-plugin:4.3.0'
}

publishing {
    repositories {
        maven {
            url "https://clojars.org/repo"
            credentials {
                username = rootProject.ext.clojarsusername
                password = rootProject.ext.clojarspassword
            }
        }
    }
    publications.withType(MavenPublication).configureEach {
        pom {
            licenses {
                license {
                    name = 'The Apache License, Version 2.0'
                    url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}
